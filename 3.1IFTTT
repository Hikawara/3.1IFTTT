#include <BH1750.h>
#include <Wire.h>
#include <WiFiNINA.h>

BH1750 lightMeter;

char ssid[] = "dlink-4B07_EXT 2.4";     // WiFi SSID
char pass[] = "bqfym76873";      // WiFi password
char webhookKey[] = "cWgGPc16X5Y1OYDiBbbfOw";  // IFTTT webhook key
WiFiClient client;

void setup() {
  Serial.begin(9600);
  Wire.begin();
  lightMeter.begin();

  Serial.println(F("BH1750 Test begin"));

  // Attempt to connect to WiFi
  connectWiFi();
}

void loop() {
  float lux = lightMeter.readLightLevel();
  Serial.print("Lux: ");
  Serial.println(lux);
  
  if (lux >= 360) {
    triggerWebhook("Sunlight");
  } else if (lux < 80) {
    triggerWebhook("Sundown");
  }
  delay(30000); // Wait for 30 seconds before reading light level again
}

void connectWiFi() {
  Serial.println("Attempting to connect to WiFi...");
  while (WiFi.status() != WL_CONNECTED) {
    WiFi.begin(ssid, pass);
    delay(5000); // Wait 5 seconds
    Serial.print(".");
  }
  Serial.println("\nConnected to WiFi");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());
}

void triggerWebhook(String eventName) {
  if (WiFi.status() != WL_CONNECTED) {
    connectWiFi();
  }
  
  if (client.connect("maker.ifttt.com", 80)) {
    Serial.println("Triggering webhook: " + eventName);
    client.println("POST /trigger/" + eventName + "/with/key/" + webhookKey + " HTTP/1.1");
    client.println("Host: maker.ifttt.com");
    client.println("Connection: close");
    client.println();
    delay(500);
    client.stop();
    Serial.println("Webhook triggered successfully");
  } else {
    Serial.println("Failed to connect to webhook server");
  }
}
